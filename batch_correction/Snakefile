
METRICS=["ARI", "ASW", "LISI_40"]
SOFTWARES=["Raw", "Raw-pp", "Seurat3", "Harmony", "LIGER", "SIMBA"]
SOFTWARES_Euc = ["Raw", "Raw-pp", "Seurat3", "Harmony", "LIGER"]
SOFTWARES_cos = ["SIMBA"]
DATASETS=["murine-atlas", "pancreas"]
SIMBA_SUFFIXES=[""]


rule convert_h5ad:
    input:
        "SIMBA_h5ad_paths.txt"
    output:
        expand("SIMBA{suffix}/output/{dataset}_SIMBA{suffix}_embedding.txt", dataset=DATASETS, suffix=SIMBA_SUFFIXES)
    shell:
        "while IFS= read -r LINE;do python ../convert_h5.py $LINE; done < {input}"

rule create_pca:
    input:
        "{software}/output/{dataset}_{software}_embedding.txt"
    output:
        "{software}/output/{dataset}_{software}_pca.txt"
    shell:
        "Rscript run_pca_on_embedding.R {input} {output} 20"

rule create_umap:
    input:
        "{software}/output/{dataset}_{software}_embedding.txt"
    output:
        "{software}/output/{dataset}_{software}_umap.txt"
    shell:
        "Rscript ../run_umap_on_embedding.R {input} {output}"

rule plot_umap:
    input:
        "{software}/output/{dataset}_{software}_umap.txt"
    output:
        "plots/{dataset}_{software}_umap_nolab.pdf"
    shell:
        "mkdir -p plots/; Rscript plots/plot_umap.R {input} {output} {wildcards.dataset}"


rule plot_umap_legend:
    input:
        "{software}/output/{dataset}_{software}_umap.txt"
    output:
        "plots/{dataset}_{software}_umap_wlab.pdf"
    shell:
        "mkdir -p plots/; Rscript plots/plot_umap.R {input} {output} {wildcards.dataset} TRUE"


rule plot_all:
    input:
        expand("plots/{dataset}_{software}_umap_nolab.pdf", software = SOFTWARES, dataset=DATASETS),
        expand("plots/{dataset}_SIMBA_umap_wlab.pdf", dataset=DATASETS),
        expand("plots/{dataset}_metrics_embedding.pdf", dataset=DATASETS)
        
rule plot_metrics:
    input:
        "metrics/{dataset}/metrics_collected_{emb}.txt"
    output:
        "plots/{dataset}_metrics_{emb}.pdf"
    shell:
        "Rscript plots/plot_metrics.R {input} {output}"

rule correct_batch:
    input:
        "data/{dataset}/{dataset}_expr.txt.gz",
        "data/{dataset}/{dataset}_metadata.txt.gz",
    params:
        output_prefix="{dataset}",
        output_dir="{software}/output/"
    output:
        "{software, (?!SIMBA)([-\w])*}/output/{dataset}_{software}_embedding.txt"
        #"{software}/output/{dataset}_{software}_embedding.txt"
    shell:
        "Rscript {wildcards.software}/run_{wildcards.software}.R {input} '{params.output_prefix}' '{params.output_dir}'| tee '{params.output_dir}/{wildcards.dataset}_log'"
       
#nPC_ft=lambda wildcards: 50 if wildcards.software == "SIMBA" else 20
#nPC_ft = 20

rule evaluate_ARI:
    input:
        "{software}/output/{dataset}_{software}_{emb}.txt"
    output:
        "metrics/{dataset}/{software}_ARI_{emb}_{sim}.txt"
    params:
        output_dir="metrics/{dataset}/",
    shell:
        "Rscript metrics/run_ARISampled.R {wildcards.software} {input} '{params.output_dir}' {wildcards.emb} {wildcards.sim}"

rule evaluate_ASW:
    input:
        "{software}/output/{dataset}_{software}_{emb}.txt"
    output:
        "metrics/{dataset}/{software}_ASW_{emb}_{sim}.txt"
    params:
        output_dir="metrics/{dataset}/",
    shell:
        "python metrics/run_ASW.py {wildcards.software} {input} {params.output_dir} {wildcards.emb} {wildcards.sim}"

rule evaluate_LISI:
    input:
        "{software}/output/{dataset}_{software}_{emb}.txt"
    output:
        "metrics/{dataset}/{software}_LISI_{plx}_{emb}_{sim}.txt" 
    params:
        output_dir="metrics/{dataset}/",
    shell: 
        "Rscript metrics/run_LISI.R {input} {params.output_dir} {wildcards.emb} {wildcards.dataset} {wildcards.software} {wildcards.plx} {wildcards.sim}"

rule collect_metrics_for_method:
    input:
        expand("metrics/{{dataset}}/{{software}}_{metric}_{{emb}}_{{sim}}.txt", metric=METRICS),
    output:
        "metrics/{dataset}/{software}_metrics_collected_{emb}_{sim}.txt"
    params:
        output_dir="metrics/{dataset}/"
    shell:
        "Rscript metrics/collect_metrics_for_method.R {wildcards.software} {input} {params.output_dir} {wildcards.emb} {wildcards.sim}"

rule collect_metrics:
    input: 
        expand("metrics/{{dataset}}/{software}_metrics_collected_{{emb}}_euclidean.txt", software=SOFTWARES_Euc), 
        expand("metrics/{{dataset}}/{software}_metrics_collected_{{emb}}_cosine.txt", software=SOFTWARES_cos)
    output:
        "metrics/{dataset}/metrics_collected_{emb}.txt"
    params:
        output_dir="metrics/{dataset}/"
    shell:
        "Rscript metrics/collect_metrics_all.R {input} {params.output_dir} {wildcards.emb}"
