ruleorder: LIGER_sort_annots > make_promoter_annot
ruleorder: create_umap > extract_umap_h5ad

METRICS=["Silhouette", "Anchoring_dist", "Cluster_agreement", "Anchoring_dist_rank_full"]
#SOFTWARES=["Raw", "Seurat3", "LIGER"]
SOFTWARES=["Raw","Seurat3", "LIGER", "SIMBA"]
PLOT_SOFTWARES=["Raw","Seurat3", "LIGER", "SIMBA"]
DATASETS=["pbmc", "mouse-skin-subset"] #"mouse-skin"


rule convert_h5ad:
    input:
        "SIMBA_h5ad_paths.txt"
    output:
        expand("SIMBA/{dataset}/integrated_embedding.txt", dataset=DATASETS)
    shell:
        "while IFS= read -r LINE;do Rscript convert_h5_matched.R $LINE; done < <(awk 'NR < 5' {input})" 


rule extract_umap_h5ad:
    input:
        "SIMBA_h5ad_paths.txt"
    output:
        expand("{{software,(SIMBA)[\w-]*}}/{dataset}/umap.txt", dataset=DATASETS) 
    shell:
        "while IFS= read -r LINE;do python extract_umap_from_h5ad.py $LINE; done < {input};"

#rule link_umap:
#    input:
#        "{software}/{dataset}/umap_from_h5ad.txt"
#    output:
#        "{software,(SIMBA)[\w-]*}/{dataset}/umap.txt"
#    shell:
#        "ln -s umap_from_h5ad.txt {output}"

rule LIGER_sort_fragments:
    input:
        "data/{dataset}/atac_fragments.bed.gz"
    output:
        "data/{dataset}/atac_fragments.sort.bed"
    threads: 20
    shell:
        "gunzip -c {input} > data/{wildcards.dataset}/atac_fragments.bed; sort -k1,1 -k2,2n -k3,3n --parallel={threads} data/{wildcards.dataset}/atac_fragments.bed > {output} ; rm data/{wildcards.dataset}/atac_fragments.bed"

rule make_promoter_annot:
    input:
        "LIGER/annots/{genome}/{genome}_genes.bed"
    output:
        "LIGER/annots/{genome}/{genome}_promoters-{upstream_length}.bed"
    shell:
        "Rscript LIGER/make_promoter_annot.R {input} {wildcards.upstream_length} {output}"

rule LIGER_sort_annots:
    input:
        "LIGER/annots/{genome}/{genome}_{element}.bed"
    output:
        "LIGER/annots/{genome}/{genome}_{element}.sort.bed"
    shell:
        "sort -k 1,1 -k2,2n -k3,3n {input} > {output}"

def genomic_element_annot(wildcards):
    dataset_to_genome_dict = { 'pbmc' : 'hg19', 'mouse-skin' : 'mm10'}
    genome = dataset_to_genome_dict[wildcards.dataset]
    return "LIGER/annots/" + genome + "/" + genome + "_{element}.sort.bed"

rule LIGER_bedmap:
    input:
        genomic_element_annot,
        "data/{dataset}/atac_fragments.sort.bed"
    output:
        "data/{dataset}/atac_{element}_bc.bed"
    shell:
        "bedmap --ec --delim '\t' --echo --echo-map-id {input} > {output}"

rule create_pca:
    input:
        "{software}/{dataset}/integrated_embedding.txt"
    output:
        "{software}_pca/{dataset}/integrated_embedding.txt"
    shell:
        "mkdir -p {wildcards.software}_pca; Rscript ../run_pca_on_embedding.R {input} {output}"

rule create_umap:
    input:
        "{software}/{dataset}/integrated_embedding.txt"
    output:
        "{software, (?!SIMBA)[\w-]+}/{dataset}/umap.txt"
    run:
        if not wildcards.software in ["SIMBA", "SIMBA-test"]:
            shell("Rscript run_umap_on_embedding.R {input} {output}")

rule plot_umap:
    input:
        "{software}/{dataset}/umap.txt"
    output:
        "plots/{dataset}_{software}_umap_nolab.pdf"
    shell:
        "mkdir -p plots/; Rscript plots/plot_umap.R {input} {output} {wildcards.dataset}"

rule plot_umap_labels:
    input:
        "{software}/{dataset}/umap.txt"
    output:
        "plots/{dataset}_{software}_umap_wlab.pdf"
    shell:
        "mkdir -p plots/; Rscript plots/plot_umap.R {input} {output} {wildcards.dataset} TRUE"

rule plot_metrics:
    input:
        "metrics/metrics_collected_{dataset}.txt"
    output:
        "plots/metric_plot_{dataset}.pdf"
    shell:
        "Rscript plots/plot_metrics.R {input} {output}"

rule plot_all:
    input:
        expand("plots/{dataset}_{software}_umap_nolab.pdf", software = PLOT_SOFTWARES, dataset=DATASETS),
        expand("plots/{dataset}_Raw_umap_wlab.pdf", dataset=DATASETS),
        expand("plots/metric_plot_{dataset}.pdf", dataset=DATASETS)


rule get_common_cells:
    input:
        expand("{software}/{{dataset}}/integrated_embedding.txt", software=SOFTWARES)
    output:
        "metrics/cells_use_{dataset}.txt"
    shell:
        "Rscript metrics/get_common_cells.R {input} {output}"

rule evaluate_metric_each:
    input:
        "{software}/{dataset}/integrated_embedding.txt",
        "metrics/cells_use_{dataset}.txt"
    output:
        "{software}/{dataset}/metric_{metric}.txt"
    params:
        output_dir="{software}/{dataset}/",
        threads=20
    shell:
        "Rscript metrics/get_metric_each.R {wildcards.metric} {input} {wildcards.software} {params.output_dir} {params.threads}"
    

rule collect_metrics_each:
    input:
        expand("{{software}}/{{dataset}}/metric_{metric}.txt", metric=METRICS)
    output:
        "{software}/{dataset}/metrics_collected.txt"
    params:
        output_dir="{software}/{dataset}/"
    shell:
        "Rscript metrics/collect_metrics_each.R {input} {params.output_dir}"




rule collect_metrics_dataset:
    input:
        expand("{software}/{{dataset}}/metrics_collected.txt", software=SOFTWARES)
    output:
        "metrics/metrics_collected_{dataset}.txt"
    params:
        output_dir="metrics/"
    shell:
        "Rscript metrics/collect_metrics_dataset.R {wildcards.dataset} {input} {params.output_dir}"


rule collect_metrics_all:
    input:
        expand("{software}/{dataset}/metrics_collected.txt", software=SOFTWARES, dataset = DATASETS)
    output:
        "metrics/metrics_collected_each.txt"
    params:
        output_dir="metrics/"
    shell:
        "Rscript metrics/collect_metrics.R {input} {params.output_dir}"


